  dependencies {
    //for dependencies found in artifact repositories you can use
    //the group:name:version notation
    compile 'commons-lang:commons-lang:2.6'
    testCompile 'org.mockito:mockito:1.9.0-rc1'
 
    //map-style notation:
    compile group: 'com.google.code.guice', name: 'guice', version: '1.0'
 
    //declaring arbitrary files as dependencies
    compile files('hibernate.jar', 'libs/spring.jar')
 
    //putting all jars from 'libs' onto compile classpath
    compile fileTree('libs')
	compile('org.hibernate:hibernate:3.1') {
	      //in case of versions conflict '3.1' version of hibernate wins:
	      force = true
	 
	      //excluding a particular transitive dependency:
	      exclude module: 'cglib' //by artifact name
	      exclude group: 'org.jmock' //by group
	      exclude group: 'org.unwanted', module: 'iAmBuggy' //by both name and group
	 
	      //disabling all transitive dependencies of this dependency
	      transitive = false
	    }

	 //configuring dependency to specific configuration of the module
    compile configuration: 'someConf', group: 'org.someOrg', name: 'someModule', version: '1.0'
 
    //configuring dependency on 'someLib' module
    compile(group: 'org.myorg', name: 'someLib', version:'1.0') {
      //explicitly adding the dependency artifact:
      artifact {
        //useful when some artifact properties unconventional
        name = 'someArtifact' //artifact name different than module name
        extension = 'someExt'
        type = 'someType'
        classifier = 'someClassifier'
      }
    }
  }
